;; output.z80 - Set the name of the console driver to use for output
;;
;; This uses the custom BIOS function we've added to the BIOS, which was never
;; present in real CP/M.  Consider it a hook into the emulator.
;;

CMDLINE:              EQU 0x80
BDOS_ENTRY_POINT:     EQU 5
BDOS_OUTPUT_STRING:   EQU 9

        ;;
        ;; CP/M programs start at 0x100.
        ;;
        ORG 100H

        ;; Copy the DMA area, which holds our command-line
        ;; flags, to a safe area at the foot of our binary.
        ;;
        LD HL, CMDLINE
        LD DE, DEST
        LD BC, 128
        LDIR

        ;;
        ;; Now we can test if we're running under cpmulator
        ;; which will trash the DMA area
        ;;
        call exit_if_not_cpmulator

        ;;
        ;; If we didn't get an argument then show the driver
        ;;
        LD      HL,DEST
        LD      A,(HL)
        INC     HL
        CP 0x00
        JR      Z,show_value


        ;; OK we're running under cpmulator, and we did get a parameter
        ;; Point DE to that and invoke the function.
        ;;
        ;;  DEST+0 contains the length of the command-line.  i.e. pascal-string
        ;;  DEST+1 contains " "
        ;;  DEST+2 contains the argument, assuming no extra space.
        ;;
        ld de, DEST+2
        ld HL, 02
        ld a, 31
        out (0xff), a

exit:
        RST 0x00


;; Show the current value.
show_value:
        ld de, CONSOLE_PREFIX       ; show a prefix
        ld c,  BDOS_OUTPUT_STRING
        call   BDOS_ENTRY_POINT

        ;; Use the given function to call a custom BIOS
        ;; routine which will the result as ASCII text
        ;; in the DMA-buffer.
        ld hl, 0x0002
        call show_bios_value

        ld de, CONSOLE_SUFFIX       ; Show a suffix
        ld c,  BDOS_OUTPUT_STRING
        call   BDOS_ENTRY_POINT
        jr exit

;;
;; Text output strings.
;;
CONSOLE_PREFIX:
        db "The output driver is set to $"
CONSOLE_SUFFIX:
        db ".", 0x0a, 0x0d, "$"

include "common.inc"

;; Copied area
DEST:

END
