;; ctrlc.asm - Get/Set the number of consecutive Ctrl-Cs required to reboot
;;
;; This uses the custom BIOS function we've added to the BIOS, which was never
;; present in real CP/M.  Consider it a hook into the emulator.
;;

BDOS_ENTRY_POINT:     EQU 5
BDOS_OUTPUT_STRING:   EQU 9
CMDLINE:              EQU 0x80   ; default DMA area too

        ;;
        ;; CP/M programs start at 0x100.
        ;;
        ORG 100H


        ;; Copy the DMA area, which holds our command-line
        ;; flags, to a safe area at the foot of our binary.
        ;;
        LD HL, CMDLINE
        LD DE, DEST
        LD BC, 128
        LDIR

        ;;
        ;; Now we can test if we're running under cpmulator
        ;; which will trash the DMA area
        ;;
        call exit_if_not_cpmulator

        ;;
        ;; If we didn't get an argument then show the current value
        ;;
        LD      HL,DEST
        LD      A,(HL)
        INC     HL
        CP      0x00
        jr z, show_value

        ;; OK we're running under cpmulator, and we did get a parameter:
        ;;
        ;;  DEST+0 contains the length of the command-line.  i.e. pascal-string
        ;;  DEST+1 contains " "
        ;;  DEST+2 contains the argument, assuming no extra space.
        ;;
        ;; Read the first digit of the parameter, only, and use it.
        ;;
        ld A,(DEST+2)
        sub '0'
        ld c, a

        ;; set the value
        ld a, 31
        ld HL, 01
        out (0xff), a

        ;; fall-through to showing the (updated) value.

show_value:
        ld de, SHOW_PREFIX
        ld c,  BDOS_OUTPUT_STRING
        call   BDOS_ENTRY_POINT

        ;; get the value
        ld a, 31
        ld HL, 01
        ld c, 0xff
        out (0xff), a

        ;; display it
        add a, '0'
        ld e, a
        ld c, 0x02
        call 0x0005

        ;; finish with a newline.
        ld DE, NEWLINE
        ld C, 0x09
        call 0x0005

        ;; Exit
        RST 0x00


;;
;; Text output strings.
;;
SHOW_PREFIX:
        db "The Ctrl-C count is currently set to $"
NEWLINE:
        db 0xa, 0xd, "$"

include "common.inc"

DEST:
END
