;; input.z80 - Set the name of the console driver to use for input.
;;
;; This uses the custom BIOS function we've added to the BIOS, which was never
;; present in real CP/M.  Consider it a hook into the emulator.
;;

BDOS_ENTRY_POINT:     EQU 5
BDOS_OUTPUT_STRING:   EQU 9
CMDLINE:              EQU 0x80   ; default DMA area too

        ;;
        ;; CP/M programs start at 0x100.
        ;;
        ORG 100H

        ;; Copy the DMA area, which holds our command-line
        ;; flags, to a safe area at the foot of our binary.
        ;;
        LD HL, CMDLINE
        LD DE, DEST
        LD BC, 128
        LDIR

        ;;
        ;; Now we can test if we're running under cpmulator
        ;; which will trash the DMA area
        ;;
        call exit_if_not_cpmulator


        ;;
        ;; If we didn't get an argument then show the driver
        ;;
        LD      HL,DEST
        LD      A,(HL)
        INC     HL
        CP 0x00
        JR      Z,show_driver


        ;; OK we're running under cpmulator, and we did get a parameter
        ;; Point DE to that and invoke the function.
        ;;
        ;;  DEST+0 contains the length of the command-line.  i.e. pascal-string
        ;;  DEST+1 contains " "
        ;;  DEST+2 contains the argument, assuming no extra space.
        ;;
        ld HL, 07
        ld de, DEST+2
        ld a, 31
        out (0xff), a

exit:
        RST 0x00


;; Show the current console driver
show_driver:
        LD DE, CONSOLE_PREFIX         ; Print a prefix
        LD C, BDOS_OUTPUT_STRING
        CALL BDOS_ENTRY_POINT

        ld HL, 07                     ; Get the driver, in the DMA area
        ld de, 0x0000
        ld a, 31
        out (0xff), a

        ld hl, 0x0080                 ; Show the contents of the DMA area, as a string
        call print_string

        LD DE, CONSOLE_SUFFIX         ; Print a suffix
        LD  C, BDOS_OUTPUT_STRING
        CALL   BDOS_ENTRY_POINT
        jr exit


;;
;; Text output strings.
;;
CONSOLE_PREFIX:
        db "The input driver is set to $"
CONSOLE_SUFFIX:
        db ".", 0x0a, 0x0d, "$"

include "common.inc"

;; Copied area
DEST:

END
