;; disable.z80 - Disable the embedded filesystem we present, and custom BIOS.
;;
;; This uses the custom BIOS function we've added to the BIOS, which was never
;; present in real CP/M.  Consider it a hook into the emulator.
;;

BDOS_ENTRY_POINT:     EQU 5
BDOS_OUTPUT_STRING:   EQU 9
CMDLINE:              EQU 0x80   ; default DMA area too

        ;;
        ;; CP/M programs start at 0x100.
        ;;
        ORG 100H

        ;; Copy the DMA area, which holds our command-line
        ;; flags, to a safe area at the foot of our binary.
        ;;
        LD HL, CMDLINE
        LD DE, DEST
        LD BC, 128
        LDIR

        ;;
        ;; Now we can test if we're running under cpmulator
        ;; which will trash the DMA area
        ;;
        call exit_if_not_cpmulator


        ;;
        ;; If we didn't get an argument then show an error
        ;;
        LD      HL,DEST
        LD      A,(HL)
        INC     HL
        CP 0x00
        jp nz, got_value    ; Got a value.

        ;; Nothing specified, show error and terminate
        LD DE, USAGE_INFORMATION
        LD C, BDOS_OUTPUT_STRING
        call BDOS_ENTRY_POINT
        jr exit


;;
;;  DEST+0 contains the length of the command-line.  i.e. pascal-string
;;  DEST+1 contains " "
;;  DEST+2 contains the argument, assuming no extra space.
;;
got_value:
        ld hl, DEST+2
        ld de, ALL
        call StrCmp
        jr z, disable_all

        ld hl, DEST+2
        ld de, BIOS
        call StrCmp
        jr z, disable_bios

        ld hl, DEST+2
        ld de, BOTH
        call StrCmp
        jr z, disable_both

        ld hl, DEST+2
        ld de, FS
        call StrCmp
        jr z, disable_fs


        ;; Unknown argument
        LD DE, WRONG_ARGUMENT
        LD C, BDOS_OUTPUT_STRING
        call BDOS_ENTRY_POINT
        ;; fall-through
exit:
        RST 0x00

disable_all:
        ld hl, 0x0009
        ld de, 0x0004
        ld a, 31
        out (0xff), a
        jr exit

disable_bios:
        ld hl, 0x0009
        ld de, 0x0002
        ld a, 31
        out (0xff), a
        jr exit

disable_both:
        ld hl, 0x0009
        ld de, 0x0003
        ld a, 31
        out (0xff), a
        jr exit

disable_fs:
        ld hl, 0x0009
        ld de, 0x0001
        ld a, 31
        out (0xff), a
        jr exit



;; strcmp: Compares string at DE with string at HL.
;; result in the Z-flag
StrCmp:
	ld a, (hl)
	cp $0
	ret z
	ld b, a
	ld a, (de)
	cp $0
	ret z
	cp b
	ret nz
	inc hl
	inc de
	jr StrCmp

;;
;; Text output strings.
;;
WRONG_ARGUMENT:
        db "Unknown argument, ignoring it.", 0x0a, 0x0d
        ;; FALL-THROUGH
USAGE_INFORMATION:
        db "Usage: DISABLE [ALL|BIOS|BOTH|FS]", 0x0a, 0x0d
        db " BIOS - Disable our BIOS extensions.", 0x0a, 0x0d
        db " FS   - Disable the embedded filesystem which hosts our extension binaries.", 0x0a, 0x0d
        db " ALL  - Disable both things, quietly.", 0x0a, 0x0d
        db " BOTH - Disable both things.", 0x0a, 0x0d
        db "$"


;; arguments are upper-cased.
ALL:
        db "ALL", 0x00
BIOS:
        db "BIOS", 0x00
BOTH:
        db "BOTH", 0x00
FS:
        db "FS", 0x00

include "common.inc"

DEST:

END
