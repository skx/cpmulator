;; hostcmd.z80 - Enable executing commands on the host
;;
;; This uses the custom BIOS function we've added to the BIOS, which was never
;; present in real CP/M.  Consider it a hook into the emulator.
;;

CMDLINE:              EQU 0x80
BDOS_ENTRY_POINT:     EQU 5
BDOS_OUTPUT_STRING:   EQU 9

        ;;
        ;; CP/M programs start at 0x100.
        ;;
        ORG 100H

        ;; Copy the DMA area, which holds our command-line
        ;; flags, to a safe area at the foot of our binary.
        ;;
        LD HL, CMDLINE
        LD DE, DEST
        LD BC, 128
        LDIR

        ;;
        ;; Now we can test if we're running under cpmulator
        ;; which will trash the DMA area
        ;;
        call exit_if_not_cpmulator


        ;;
        ;; If we didn't get an argument then show the current value
        ;;
        LD      HL,DEST
        LD      A,(HL)
        INC     HL
        CP 0x00
        JR      Z,show_value

        ;; OK we're running under cpmulator, and we did get a parameter
        ;; Point DE to that and invoke the function.
        ;;
        ;;  DEST+0 contains the length of the command-line.  i.e. pascal-string
        ;;  DEST+1 contains " "
        ;;  DEST+2 contains the argument, assuming no extra space.
        ;;
        ld HL, 08
        ld de, DEST + 2
        ld a, 31
        out (0xff), a

exit:
        RST 0x00

;; Show the current value
show_value:
        ;; just set the first byte of the DMA area to null
        LD HL, 0x0080
        LD (HL), 0x00

        ld HL, 08
        ld de, 0x0000
        ld a, 31
        out (0xff), a

        ;; Now the DMA area, defaulting to 0x0080
        ;; will contain the value which is set.
        LD HL, 0x0080

        ;; Is the value null?
        LD A, (HL)
        CP 0
        jr z, unset

        LD DE, OUTPUT_PREFIX         ;; Show a prefix
        LD C, BDOS_OUTPUT_STRING
        CALL BDOS_ENTRY_POINT


        LD HL, 0x0080                ;; Show the contents of the DMA area, as a string
        call print_string

        LD DE, OUTPUT_SUFFIX         ;; Show a suffix
        LD C, BDOS_OUTPUT_STRING
        CALL BDOS_ENTRY_POINT
        jr exit


;; The host command-prefix is unset
unset:
        LD DE, UNSET_STRING
        LD C, BDOS_OUTPUT_STRING
        CALL BDOS_ENTRY_POINT
        jr exit


;;
;; Text output strings.
;;
UNSET_STRING:
        db "The prefix for executing commands on the host is unset.", 0x0a, 0x0d
        db "Running commands on the host is disabled.", 0x0a, 0x0d, "$"
OUTPUT_PREFIX:
        db "The command-prefix for executing commands on the host is '$"
OUTPUT_SUFFIX:
        db "'.", 0x0a, 0x0d
        db "Run '!hostcmd /clear' to disable running commands on the host", 0x0a, 0x0d, "$"

include "common.inc"


;; Copied area
DEST:
END
